import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka' // Must be applied here for publish plugin.

kotlin {
  android {
    publishAllLibraryVariants()
  }
  js {
    browser()
  }
  jvm()

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlinx.coroutines.core
        api project(':compose:runtime')
        api project(':treehouse-protocol')
        implementation project(':treehouse-widget')
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test
        implementation project(':treehouse-test-schema:compose')
      }
    }
    androidMain {
      dependencies {
        implementation deps.androidx.core
      }
    }
    androidTest {
      dependsOn jvmTest
    }
  }
}

android {
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

dependencies {
  add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler'))
}

spotless {
  kotlin {
    targetExclude(
      // Apache 2-licensed files from AOSP.
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiDispatcher.kt",
      "src/androidMain/kotlin/app/cash/treehouse/compose/AndroidUiFrameClock.kt",
    )
  }
}
