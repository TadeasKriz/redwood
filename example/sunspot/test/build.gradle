apply plugin: 'org.jetbrains.kotlin.multiplatform'

def generatedDir = 'build/generated/treehouse'

kotlin {
  js {
    browser()
  }
  jvm()

  sourceSets {
    commonMain {
      kotlin {
        srcDir generatedDir
      }
      dependencies {
        api project(':example:sunspot')
        api project(':example:sunspot:widget')
      }
    }
  }
}

configurations {
  generator
}

dependencies {
  generator project(':treehouse-schema-generator')
  generator project(':example:sunspot')
}

def generate = tasks.register('treehouseGenerate', JavaExec) {
  it.classpath(configurations.generator)
  it.main('app.cash.treehouse.schema.generator.Main')
  it.args(
    '--test',
    '--out', generatedDir,
    'example.sunspot.Sunspot',
  )

  it.doFirst {
    file(generatedDir).deleteDir()
  }
}

kotlin.targets.all { target ->
  target.compilations.all { compilation ->
    compilation.compileKotlinTask.dependsOn(generate.get())
  }
}
