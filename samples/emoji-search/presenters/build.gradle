apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.zipline'
apply plugin: 'app.cash.redwood'

kotlin {
  iosArm64()
  iosX64()
  iosSimulatorArm64()

  jvm()

  js {
    browser()
    binaries.executable()
  }

  sourceSets {
    commonMain {
      dependencies {
        api libs.zipline
        api projects.emojiSearch.schema.compose
        api 'app.cash.redwood:redwood-treehouse'
        api 'app.cash.redwood:redwood-protocol-compose'
        implementation projects.emojiSearch.schema.compose.protocol
      }
    }

    hostMain {
      dependsOn(commonMain)
      dependencies {
        implementation libs.zipline.loader
        api libs.okio
      }
    }

    jvmMain {
      dependsOn(hostMain)
      dependencies {
        implementation libs.okHttp
      }
    }

    iosMain {
      dependsOn(hostMain)
    }
    targets.all { target ->
      // Some Kotlin targets do not have this property, but native ones always will.
      if (target.hasProperty('platformType') && target.platformType.name == "native") {
        target.compilations.main.source(sourceSets.iosMain)
      }
    }
  }
}

zipline {
  mainFunction = 'app.cash.zipline.samples.emojisearch.preparePresenters'
}
